// Terminals
DAYS.3: /\d+d/
DATE.2: /\d{1,2}\.\d{1,2}(\.\d{2}(\d{2})?)?/
INT.1: /\d+/

// Rules
?start: sum
      | NAME "=" sum            -> assignment

?sum: product
    | sum "+" product           -> add
    | sum "-" product           -> sub

?product: atom
        | product "*" atom      -> mul
        | product "/" atom      -> div

?atom: DAYS                     -> days
     | DATE                     -> date
     | NAME                     -> var
     | INT                      -> number
     | "(" sum ")"

%import common.CNAME            -> NAME
%import common.WS_INLINE

%ignore WS_INLINE